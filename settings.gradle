import java.util.regex.Matcher

/*
 * Copyright 2022 Google LLC
 * Copyright 2023-2024 wintmain
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * NOTE: This file has been modified by wintmain
 * Modifications are licensed under the License.
 */

include ':app-catalog:app'
// Dynamically include samples under /app-catalog/samples/ folder
def samples = []
// Find all build.gradle files under samples folder
settingsDir.traverse(nameFilter: "build.gradle") {
    def path = it.parent.substring(rootDir.path.length())
    def separator = File.separator
    if (path.contains("${separator}app-catalog${separator}samples${separator}")) {
        // convert the module path into gradle module notation
        samples.add(path.replaceAll(Matcher.quoteReplacement(separator), ":"))
    }
}
// include all available samples and store it in the global variable.
println("Included samples: $samples")
include(*samples)
gradle.ext.samples = samples

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenCentral()
        google()

        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public/' }
        maven { url 'https://maven.aliyun.com/repository/central' }

        // JitPack 远程仓库：https://jitpack.io, 用来解析Github上的三方库
        maven { url 'https://jitpack.io' }

        // Uncomment this to use a snapshot version of casa-android.
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
}
